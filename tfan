#define echoInput PA5
#define echoTrigger PA6
#define fanControl PA7
#define manualButton PA8
#define LED PC13

#define console Serial.println

unsigned long currentTick = 0;
unsigned long sonicCounterPreviousTick = 0;
unsigned long reverseCounterPreviousTick = 0;
unsigned long buttonLastTimePressedTick = 0;
unsigned long fanEnableStartTimeTick = 0;
unsigned long ledLastTimeEnableTick = 0;

unsigned long hardwareRestartTime = 14 * 24 * 60 * 60 * 1000;  //first number is count of days
unsigned long pauseTime = 1 * 1000;                            //1 second

float dist = 0;

uint8_t counter = 0;
uint8_t buttonPressedCount = 0;

uint32_t fanEnableTime = 2 * 60 * 1000;  //2 minutes

bool isEntityDetected = false;
bool isFanEnabled = false;
bool isNeedToEnableFan = false;
bool isSystemDisabled = false;
bool isButtonPressed = false;

void setup() {
  // put your setup code here, to run once:
  Serial.begin(115200);
  pinMode(echoInput, INPUT);
  pinMode(echoTrigger, OUTPUT);
  pinMode(LED, OUTPUT);
  pinMode(fanControl, OUTPUT);
  pinMode(manualButton, INPUT);

  digitalWrite(echoTrigger, LOW);
  digitalWrite(fanControl, LOW);
  digitalWrite(LED, HIGH);

  attachInterrupt(manualButton, buttonPressed, FALLING);
}

void loop() {
  currentTick = millis();

  sonicCounter();
  reverseCounter();
  enableFan();

  buttonHandler();
  checkWorkTime();
}

void sonicCounter() {
  if (isSystemDisabled) {
    return;
  }
  if (!isEntityDetected && !isNeedToEnableFan && (currentTick - sonicCounterPreviousTick) > pauseTime) {
    dist = getDist();
    sonicCounterPreviousTick = currentTick;
    if (dist < 100) {
      ledBlink();
      counter++;
      console(" counter++ " + (String)counter + " " + (String)dist);
    } else {
      if (counter > 0) {
        counter--;
        console("counter-- " + (String)counter + " " + (String)dist);
      }
    }
  }
  if (counter > 9) {
    isEntityDetected = true;
  }
}

void reverseCounter() {
  if (isSystemDisabled) {
    return;
  }
  if (isEntityDetected && !isNeedToEnableFan && (currentTick - reverseCounterPreviousTick) > pauseTime) {
    reverseCounterPreviousTick = currentTick;
    dist = getDist();
    if (dist > 100) {
      ledBlink();
      delay(50);
      ledBlink();
      counter--;
      console(" counter-- " + (String)counter + " " + (String)dist);
      if (counter < 1) {
        counter = 0;
        isNeedToEnableFan = true;
        isEntityDetected = false;
      }
    } else {
      counter = 10;
    }
  }
}

void enableFan() {
  if (isSystemDisabled) {
    goto interruptedFanFlag;
  }
  if (isNeedToEnableFan) {
    if (!isFanEnabled) {
      digitalWrite(fanControl, HIGH);
      isFanEnabled = true;
      fanEnableStartTimeTick = currentTick;
      console("fan enabled");
    }
    if ((currentTick - fanEnableStartTimeTick) > fanEnableTime) {
      console("fan disabled");
interruptedFanFlag:
      if (isFanEnabled) {
        digitalWrite(fanControl, LOW);
        isFanEnabled = false;
      }
      isNeedToEnableFan = false;
    }
  } else {
    goto interruptedFanFlag;
  }
}

float getDist() {
  //pause to prevert problems with echo
  delay(50);
  // 10mks impulse
  digitalWrite(echoTrigger, HIGH);
  delayMicroseconds(10);
  digitalWrite(echoTrigger, LOW);

  // echo impulse time measure
  uint32_t us = pulseIn(echoInput, HIGH);

  // count time and return
  return us / 58.2;
}


void buttonPressed() {
  console("button pressed interrupterd");
  if ((currentTick - buttonLastTimePressedTick) > 220) {
    buttonLastTimePressedTick = currentTick;
    buttonPressedCount++;
    isButtonPressed = true;
    console(buttonPressedCount);
  }
}

void buttonHandler() {
  if (isButtonPressed) {
    if ((currentTick - buttonLastTimePressedTick) > 2000) {
      switch (buttonPressedCount) {
        case 1:
          {
            console("handle button case 1");
            isNeedToEnableFan = true;
            fanEnableStartTimeTick = currentTick;
            break;
          }
        case 2:
          {
            console("handle button case 2");
            isNeedToEnableFan = false;
            isEntityDetected = false;
            counter = 0;
            break;
          }
        case 3:
          {
            console("handle button case 3");
            isSystemDisabled = true;
            digitalWrite(LED, LOW);
            break;
          }
        default:
          {
            console("handle button case default and clear interrupted flag");
            isSystemDisabled = false;
            digitalWrite(LED, HIGH);
            break;
          }
      }
      isButtonPressed = false;
      buttonPressedCount = 0;
    }
  }
}

void checkWorkTime() {
  if (currentTick > hardwareRestartTime) {
    HAL_NVIC_SystemReset();
  }
}

void ledBlink() {
  //FUUUUUUUUUU
  if (digitalRead(LED) == HIGH) {
    digitalWrite(LED, LOW);
    delay(25);
    digitalWrite(LED, HIGH);
  } else {
    digitalWrite(LED, HIGH);
    delay(25);
    digitalWrite(LED, LOW);
  }
}
